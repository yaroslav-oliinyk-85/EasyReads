plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'com.google.devtools.ksp'
    id 'androidx.navigation.safeargs'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas".toString())
    arg("room.generateKotlin", "true")
}

def localProps = new Properties()
def localPropsFile = rootProject.file("local.properties")
if (localPropsFile.exists()) {
    localPropsFile.withInputStream { stream ->
        localProps.load(stream)
    }
}

static def getProp(String key, Properties props) {
    return System.getenv(key) ?: props.getProperty(key)
}

android {
    namespace 'com.oliinyk.yaroslav.easyreads'
    compileSdk 36

    defaultConfig {
        applicationId "com.oliinyk.yaroslav.easyreads"
        minSdk 24
        targetSdk 35
        versionCode 14
        versionName "1.0.0-b14"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    signingConfigs {
        create("release") {
            storeFile = file(getProp("RELEASE_STORE_FILE", localProps))
            storePassword = getProp("RELEASE_STORE_PASSWORD", localProps)
            keyAlias = getProp("RELEASE_KEY_ALIAS", localProps)
            keyPassword = getProp("RELEASE_KEY_PASSWORD", localProps)
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig = signingConfigs.getByName("release")
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17

        // Required for java.time on older devices
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.14"
    }
    buildToolsVersion '35.0.00'
}

composeCompiler {
    reportsDestination = layout.buildDirectory.dir("compose_compiler")
    stabilityConfigurationFile = rootProject.layout.projectDirectory.file("stability_config.conf")
}

dependencies {
    implementation 'com.google.firebase:firebase-crashlytics:20.0.4'
    implementation 'com.google.firebase:firebase-messaging:25.0.1'

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_jdk_libs_version"

    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'com.google.android.material:material:1.13.0'
    //ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    //Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    //Lifecycle Compose
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    //Activity Compose
    implementation "androidx.activity:activity-compose:$activity_version"
    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    //Navigation Compose
    implementation "androidx.navigation:navigation-compose:$navigation_version"
    //Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    //DataStore
    implementation 'androidx.datastore:datastore-preferences:1.2.0'
    //Dagger Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel-compose:$hilt_compose_version"

    //Compose UI
    implementation(platform("androidx.compose:compose-bom:$compose_bom_version"))
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-graphics")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose")
    implementation("androidx.compose.material:material-icons-core")
    implementation("androidx.compose.material:material-icons-extended")

    //Coil Compose
    implementation "io.coil-kt:coil-compose:$coil_compose_version"

    //Serialization Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"

    //Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

    //Tests Compose
    androidTestImplementation(platform("androidx.compose:compose-bom:$compose_bom_version"))
    androidTestImplementation("androidx.compose.ui:ui-test-junit4")
    debugImplementation("androidx.compose.ui:ui-tooling")
    debugImplementation("androidx.compose.ui:ui-test-manifest")
}
